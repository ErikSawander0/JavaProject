plugins {
    id 'java'
    id 'war'
    id "com.bmuschko.cargo" version "2.8.0"
}

// https://payara.fish/blog/deploying-to-payara-server-using-gradle/

group 'com.miun.martinClass'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.13.2'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// https://stackoverflow.com/questions/21999829/how-do-i-read-properties-defined-in-local-properties-in-build-gradle
def Properties properties = new Properties()
properties.load(project.rootProject.file("local.properties").newDataInputStream())

// Configure the cargo related properties.
// Configured to -P provided options, then properties file, then environment variable, then default
ext.setProperty("payaraHome", project.findProperty('payaraHome') ?: System.getenv("PAYARA_HOME") ?: properties.getProperty("payaraHome"));
ext.setProperty("payaraHostname", project.findProperty('payaraHostname') ?: "localhost");
ext.setProperty("payaraUsername", project.findProperty('payaraUsername') ?: "admin");
ext.setProperty("payaraPassword", project.findProperty('payaraPassword') ?: "");

dependencies {
    compileOnly('jakarta.platform:jakarta.jakartaee-api:11.0.0')

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")

    implementation("com.h2database:h2:2.2.224")
    providedCompile 'jakarta.platform:jakarta.jakartaee-api:10.0.0'
}

// Configure Cargo Plugin
cargo {
    // Configures which commands require a built artifact
    cargoRunLocal.dependsOn war
    cargoStartLocal.dependsOn war
    cargoDeployRemote.dependsOn war
    cargoRedeployRemote.dependsOn war
    cargoRedeployLocal.dependsOn war

    containerId = 'glassfish5x'

    deployable {
        context = "/"
    }

    local {
        homeDir = file(payaraHome)
    }
}

// Configure the asadmin executable location
ext.setProperty('asadminDir', "${payaraHome}${File.separator}bin");
ext.setProperty('asadminFile', System.properties['os.name'].toLowerCase().contains('windows')? ['cmd', '/c', 'asadmin.bat']: './asadmin')

// Define Gradle tasks to perform similar functions to the Cargo plugin
task startServer(type:Exec) {
    workingDir asadminDir;
    commandLine asadminFile;

    args "start-domain"
}

task stopServer(type:Exec) {
    workingDir asadminDir;
    commandLine asadminFile;

    args "stop-domain"
}

task redeploy(dependsOn: 'war', type:Exec) {
    workingDir asadminDir;
    commandLine asadminFile;

    args "deploy", "--force=true", "--contextroot=/", "--name=cargoApp", "${war.archivePath}";
}

test {
    useJUnitPlatform()
}